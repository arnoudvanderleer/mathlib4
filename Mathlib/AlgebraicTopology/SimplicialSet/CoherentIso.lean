/-
Copyright (c) 2024 Johns Hopkins Category Theory Seminar. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johns Hopkins Category Theory Seminar, Arnoud van der Leer
-/
module

public import Mathlib.AlgebraicTopology.SimplicialSet.CompStruct
public import Mathlib.AlgebraicTopology.SimplicialSet.StrictSegal

/-!
# The Coherent Isomorphism

In this file, we define two related types.

We first define the free walking or free-living isomorphism `WalkingIso`: the category with
two objects `false` and `true`, and morphisms `false ‚ü∂ true` and `true ‚ü∂ false`.
We show that the type of functor from `WalkingIso` into any category is equivalent to the type of
isomorphisms in that category.

Then we define the simplicial set `coherentIso` as the nerve of `WalkingIso`.
Since the morphism types in `WalkingIso` are given by `unit`, the `n`-simplices of `coherentIso` are
equivalent to `Fin 2`-vectors of length `n + 1`. This shows that the `n`-simplices of `coherentIso`
have decidable equality.
Lastly, we show that `hom : coherentIso _‚¶ã1‚¶å` (the edge from `false` to `true`) has an inverse,
and `invStructOfEqMapHom` concludes from this that for any simplicial set `X`,
any morphism `g : coherentIso ‚ü∂ X` and any `f : X _‚¶ã1‚¶å`,
if `g` sends `hom` to `f`, then `f` has an inverse.

-/

@[expose] public section

universe u v w

open CategoryTheory

namespace CategoryTheory

/-- This is the free-living isomorphism as a category with objects `false` and `true`. -/
inductive WalkingIso : Type u where
  | zero : WalkingIso
  | one  : WalkingIso

attribute [aesop safe cases (rule_sets := [CategoryTheory])] WalkingIso
attribute [grind cases] WalkingIso

namespace WalkingIso

/-- The underlying type of `WalkingIso` is equivalent to `Bool`, since they both have 2 elements. -/
def equivBool : WalkingIso.{u} ‚âÉ Bool where
  toFun := fun
    | .zero => false
    | .one => true
  invFun := fun
    | false => .zero
    | true => .one
  left_inv := by
    rintro (_ | _) <;>
    rfl
  right_inv := by
    rintro (_ | _) <;>
    rfl

instance : DecidableEq WalkingIso.{u} :=
  fun _ _ ‚Ü¶ decidable_of_iff _ (Equiv.apply_eq_iff_eq equivBool)

/-- The free isomorphism is the codiscrete category on two objects. -/
instance : Category.{0, u} WalkingIso.{u} where
  Hom _ _ := Unit
  id _ := ‚ü®‚ü©
  comp _ _ := ‚ü®‚ü©

instance homUnique {x y : WalkingIso.{u}} : Unique (x ‚ü∂ y) := inferInstanceAs (Unique Unit)

section

variable {C : Type u} [Category.{v} C]

/-- The isomorphism at the heart of `WalkingIso` -/
def iso : zero ‚âÖ one where
  hom := ()
  inv := ()

@[simp]
lemma eq_iso_hom (f : zero ‚ü∂ one) : f = iso.hom := rfl

@[simp]
lemma eq_iso_inv (f : one ‚ü∂ zero) : f = iso.inv := rfl

/-- Functors out of `WalkingIso` define isomorphisms in the target category. -/
@[simps!]
def toIso (F : WalkingIso.{w} ‚•§ C) : F.obj zero ‚âÖ F.obj one := F.mapIso iso

/-- From an isomorphism in a category, true can build a functor out of `WalkingIso` to
  that category. -/
def fromIso {X Y : C} (e : X ‚âÖ Y) : WalkingIso.{w} ‚•§ C where
  obj := fun
    | zero => X
    | one => Y
  map := @fun
    | zero, zero, _ => ùüô _
    | zero, one,  _ => e.hom
    | one, zero, _ => e.inv
    | one, one,  _ => ùüô _
  map_comp := by rintro (_ | _) (_ | _) (_ | _) <;> simp

section

variable {X Y : C} (e : X ‚âÖ Y)

@[simp]
lemma fromIso_zero : (fromIso e).obj .zero = X := rfl

@[simp]
lemma fromIso_one : (fromIso e).obj .one = Y := rfl

@[simp]
lemma fromIso_map_zero_zero (f : zero ‚ü∂ zero) : (fromIso e).map f = ùüô X := rfl

@[simp]
lemma fromIso_hom (f : zero ‚ü∂ one) : (fromIso e).map f = e.hom := rfl

@[simp]
lemma fromIso_inv (f : one ‚ü∂ zero) : (fromIso e).map f = e.inv := rfl

@[simp]
lemma fromIso_map_one_one (f : one ‚ü∂ one) : (fromIso e).map f = ùüô Y := rfl

end

/-- An equivalence between the type of `WalkingIso`s in `C` and the type of isomorphisms in `C`. -/
@[simps]
def equiv : (WalkingIso.{w} ‚•§ C) ‚âÉ Œ£ (X : C) (Y : C), (X ‚âÖ Y) where
  toFun F := ‚ü®F.obj zero, F.obj one, toIso F‚ü©
  invFun p := fromIso p.2.2
  right_inv := fun ‚ü®X, Y, e‚ü© ‚Ü¶ rfl
  left_inv F := by
    apply Functor.hext
    ¬∑ rintro (_ | _) <;> rfl
    ¬∑ rintro (_ | _) (_ | _) (_) <;>
    ( rw [heq_eq_eq]
      unfold fromIso toIso
      dsimp
      try rw [‚Üê F.map_id]
      rfl )

end

end WalkingIso

end CategoryTheory

namespace SSet

open Simplicial Edge

/-- The simplicial set that encodes a single isomorphism.
  Its n-simplices are formal compositions of arrows in WalkingIso. -/
def coherentIso : SSet := nerve WalkingIso.{u}

namespace coherentIso

instance : IsStrictSegal coherentIso := inferInstanceAs (IsStrictSegal (nerve _))

/-- Since the morphisms in WalkingIso do not carry information, an n-simplex of coherentIso
  is equivalent to an (n + 1)-vector of the objects of WalkingIso. -/
def equivFun {n : ‚Ñï} : coherentIso _‚¶ãn‚¶å ‚âÉ (Fin (n + 1) ‚Üí WalkingIso.{u}) where
  toFun f := f.obj
  invFun f := .mk f (fun _ ‚Ü¶ ‚ü®‚ü©) (fun _ ‚Ü¶ rfl) (fun _ _ ‚Ü¶ rfl)

/-- Since `Bool` (and hence `WalkingIso`) has decidable equality,
  the simplices of coherentIso have decidable equality as well. -/
instance (n : ‚Ñï) : DecidableEq (coherentIso.{u} _‚¶ãn‚¶å) :=
  fun _ _ ‚Ü¶ decidable_of_iff _ (Equiv.apply_eq_iff_eq coherentIso.equivFun)

/-- The source vertex of `coherentIso`. -/
def x‚ÇÄ : coherentIso _‚¶ã0‚¶å :=
  ComposableArrows.mk‚ÇÄ WalkingIso.zero.{u}

/-- The target vertex of `coherentIso`. -/
def x‚ÇÅ : coherentIso _‚¶ã0‚¶å :=
  ComposableArrows.mk‚ÇÄ WalkingIso.one.{u}

/-- The forwards edge of `coherentIso`. -/
def hom : Edge.{u} x‚ÇÄ x‚ÇÅ where
  edge := ComposableArrows.mk‚ÇÅ WalkingIso.iso.hom
  src_eq := ComposableArrows.ext‚ÇÄ rfl
  tgt_eq := ComposableArrows.ext‚ÇÄ rfl

/-- The backwards edge of `coherentIso`. -/
def inv : Edge.{u} x‚ÇÅ x‚ÇÄ where
  edge := ComposableArrows.mk‚ÇÅ WalkingIso.iso.inv
  src_eq := ComposableArrows.ext‚ÇÄ rfl
  tgt_eq := ComposableArrows.ext‚ÇÄ rfl

/-- The forwards and backwards edge of `coherentIso` compose to the identity. -/
def homInvId : Edge.CompStruct.{u} hom inv (Edge.id x‚ÇÄ) where
  simplex := ComposableArrows.mk‚ÇÇ WalkingIso.iso.hom WalkingIso.iso.inv
  d‚ÇÇ := ComposableArrows.ext‚ÇÅ rfl rfl rfl
  d‚ÇÄ := ComposableArrows.ext‚ÇÅ rfl rfl rfl
  d‚ÇÅ := ComposableArrows.ext‚ÇÅ rfl rfl rfl

/-- The backwards and forwards edge of `coherentIso` compose to the identity. -/
def invHomId : Edge.CompStruct.{u} inv hom (Edge.id x‚ÇÅ) where
  simplex := ComposableArrows.mk‚ÇÇ WalkingIso.iso.inv WalkingIso.iso.hom
  d‚ÇÇ := ComposableArrows.ext‚ÇÅ rfl rfl rfl
  d‚ÇÄ := ComposableArrows.ext‚ÇÅ rfl rfl rfl
  d‚ÇÅ := ComposableArrows.ext‚ÇÅ rfl rfl rfl

/-- The forwards edge of `coherentIso` has an inverse. -/
@[simps]
def invStructHom : Edge.InvStruct.{u} coherentIso.hom where
  inv := inv
  homInvId := homInvId
  invHomId := invHomId

/-- If an edge is equal to the image of `hom` under an SSet morphism,
  this edge has an inverse. -/
def invStructOfEqMapHom {X : SSet.{u}} {x‚ÇÄ x‚ÇÅ : X _‚¶ã0‚¶å}
    {f : Edge x‚ÇÄ x‚ÇÅ}
    {g : coherentIso ‚ü∂ X}
    (hfg : f.edge = g.app _ hom.edge) :
    f.InvStruct :=
  (invStructHom.map g).ofEq hfg.symm

end coherentIso

end SSet
